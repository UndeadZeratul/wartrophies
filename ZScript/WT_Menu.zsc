// [Ace] Each time I have to create somethingsomethingMenu.zsc I feel like drinking myself to death.

class OptionMenuItemFolderOption : OptionMenuItemOption
{
	override void SetSelection(int Selection)
	{
		Super.SetSelection(Selection);

		EventHandler.SendNetworkEvent("WT_FolderInit");
		EventHandler.SendNetworkEvent("WT_MarkNotUpdated", WT_MainHandler.Update_Lite);
	}
} 

class OptionMenuItemFilterOption : OptionMenuItemOption
{
	override void SetSelection(int Selection)
	{
		Super.SetSelection(Selection);

		EventHandler.SendNetworkEvent("WT_MarkNotUpdated", WT_MainHandler.Update_All);
	}
}

class OptionMenuItemOptionCustomFont : OptionMenuItemOption
{
	protected void CustomDrawValue(int indent, int y, int color, String text, bool grayed = false)
	{
		String label = Stringtable.Localize(text);
		int overlay = grayed? Color(96,48,0,0) : 0;
		Font fnt = WT_MainHandler.FontName;
		screen.DrawText(fnt, color, indent + CursorSpace(), y + fnt.GetHeight() + 2, text, DTA_CleanNoMove_1, true, DTA_ColorOverlay, overlay);
	}

	override int Draw(OptionMenuDescriptor desc, int y, int indent, bool selected)
	{
		if (mCenter)
		{
			indent = (screen.GetWidth() / 2);
		}
		drawLabel(indent, y, selected? OptionMenuSettings.mFontColorSelection : OptionMenuSettings.mFontColor, isGrayed());

		int Selection = GetSelection();
		String text = StringTable.Localize(OptionValues.GetText(mValues, Selection));
		if (text.Length() == 0) text = "Unknown";
		CustomDrawValue(indent, y, OptionMenuSettings.mFontColorValue, text, isGrayed());
		return indent;
	}
}

class OptionMenuWTBlacklistMenu : OptionMenu
{
	override void Init(Menu parent, OptionMenuDescriptor desc)
	{
		Super.Init(parent, desc);
		mParentMenu = parent;
		mDesc = desc;

		let MainHandler = WT_MainHandler(EventHandler.Find("WT_MainHandler"));
		if (MainHandler)
		{
			// Prevents item duplication in menu when re-entering it.
			mDesc.mItems.Delete(5, MainHandler.BlacklistDefs.Size() / 3);

			for (int i = 0; i < MainHandler.BlacklistDefs.Size(); i += 3)
			{
				let StaticTextItem = new ("OptionMenuItemStaticText");
				string Text = "\c[White]Mod: \c[Tan]"..MainHandler.BlacklistDefs[i].."\c[White] - Name: \c[Tan]"..MainHandler.BlacklistDefs[i + 2].."\c-";
				mDesc.mItems.Push(StaticTextItem.InitDirect(Text, Font.CR_UNTRANSLATED));
			}
		}
	}
}